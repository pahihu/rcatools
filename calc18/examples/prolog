..
..  PROLOG
..
DMA=#00
INT=#01
INTSP=#02
PC=#03
SCALL=#04
SRET=#05
ARGPTR=#06
SP=#07
AC=#08
RETVAL=#08
AUX=#09
SUB=#0D
VARPTR=#0E
MQ=#0E
FP=#0F
SSEG=#7FFF
..
 ORG#0000

 DIS,#33                        ..X=P=0
 OUT 1,#01                      ..TWO-LEVEL I/O
 LDI A.1(SSEG) ;PHI SP          ..SP=SSEG
 LDI A.0(SSEG) ;PLO SP
 SEX SP                         ..X=SP
 LDI A.1(CALL) ;PHI SCALL       ..SCALL
 LDI A.0(CALL) ;PLO SCALL
 LDI A.1(RET) ;PHI SRET         ..SRET
 LDI A.0(RET) ;PLO SRET
 SEP SCALL,A(Lmain)             ..main()
 SEP SCALL,A(Lexit)             ..exit()
 ..
 ..  PUTC
 ..
RPUTC:
 SEP PC
PUTC:
 GLO AC ;STR SP
 OUT 2 ;DEC SP
 BR RPUTC
..
..  MULT AC BY AUX.0
..
RUMULT:
 SEP PC
UMULT:
 GHI AC ;STXD            .. *SP=AC
 GLO AC ;STR SP
 INC SP
 GLO AUX ;PLO AC         .. AC.0=AUX.0
 LDI #00 ;PLO MQ ;PHI AC .. MQ.0=AC.1=0
 LDI #08 ;PLO AUX        .. AUX.0=8
M1:
 GLO AC ;SHR             .. IF AC.0 & 1 = 0 GOTO DIV2
 BNF DIV2
 DEC SP                  .. --SP
 GHI AC       ;ADD ;PHI AC
 GLO MQ ;IRX  ;ADC ;PLO MQ  .. (MQ.0,AC.1) += *SP
DIV2:
 GLO MQ ;SHR  ;PLO MQ    .. (MQ.0,AC.1,AC.0) >> 1
 GHI AC ;SHRC ;PHI AC
 GLO AC ;SHRC ;PLO AC
 DEC AUX                 .. IF --AUX != 0 GOTO LOOP
 GLO AUX ;BNZ M1
 BR RUMULT
..
..  DIVIDE AC BY AUX.0
..
RUDIV:
 SEP PC
UDIV:
 GLO AUX ;STR SP         .. *SP = AUX.0
 LDI #00 ;PLO MQ
 LDI #10 ;PLO AUX        .. AUX.0 = 16
M2:
 GLO AC ;SHL ;PLO AC     .. (MQ.0,AC) << 1
 GHI AC ;SHLC ;PHI AC
 GLO MQ ;SHLC ;PLO MQ
 SM                      .. IF MQ - *SP < 0 GOTO NEG
 BNF NEG
 PLO MQ                  .. MQ = MQ - *SP
 GLO AC ;ADI #01 ;PLO AC .. AC = AC + 1
NEG:
 DEC AUX
 GLO AUX ;BNZ M2
 GLO MQ ;PLO AUX         .. AUX.0 = REM
 BR RUDIV
..
..  STANDARD CALL ROUTINE
..
RCALL: SEP PC
CALL:
 GHI ARGPTR ;STXD        ..PUSH ARGPTR
 GLO ARGPTR ;STXD
 GHI PC ;PHI ARGPTR      ..ARGPTR=PC
 GLO PC ;PLO ARGPTR
 LDA ARGPTR ;PHI PC      ..PC=*ARGPTR
 LDA ARGPTR ;PLO PC
 BR RCALL                ..BRANCH TO ENTRY
..
..  STANDARD RETURN ROUTINE
..
RRET: SEP PC
RET:
 GHI ARGPTR ;PHI PC      ..PC=ARGPTR
 GLO ARGPTR ;PLO PC
 SEX SP                  ..X=SP
 INC SP
 LDXA ;PLO ARGPTR        ..POP ARGPTR
 LDX  ;PHI ARGPTR
 GHI RETVAL
 BR RRET
..
..  PROGRAM
..
