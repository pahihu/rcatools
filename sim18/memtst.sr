..
..  ELF/OS STYLE MEMORY TESTER.
..
..  R4 CONTAINS '600D' IF ALL TESTS PASSED
..  ELSE 'DEAD' WHEN A TEST WENT WRONG
..
PTR=#02         .. MEMORY LOCATION
PC=#03
TST=#07         .. TEST#
VAL=#08         .. VALUE WRITTEN
SUB=#09         .. SUBR. ADDRESS

        ORG#0000

        DIS,#33                 .. X=P=3
        SEX PTR                 .. X=PTR
        GHI PC ;PLO TST         .. CLEAR TST,PTR,SUB
        PHI PTR ;PHI SUB
..
..  1. WRITE 00 TO ALL LOCATIONS AND CHECK
..
        LDI A.0(WRALL) ;PLO SUB .. SUB=WRALL
        GHI PC ;SHR ;SEP SUB    .. D=#00, DF=0, CALL WRALL
..
..  2. WRITE FF TO ALL LOCATIONS AND CHECK
..
        LDI #FF ;SEP SUB        .. D=#FF, CALL WRALL
..
..  3. WRITE SEQ NUMBER TO ALL CELLS
..
        SMI #00 ;SEP SUB        .. DF=1, CALL WRALL
..
..  4. WRITE PAGE NUMBER TO ALL CELLS
..
        LDI A.0(WRPAG); PLO SUB .. SUB=WRPAG
        SEP SUB                 .. DF STILL 0, CALL WRPAG
..
..  5. WRITE INVERSE PAGE NUMBER TO ALL CELLS
..
        SMI #00 ;SEP SUB        .. DF=1, CALL WRPAG
..
..  6. WRITE ALL VALUES TO ALL CELLS
..
TST6:   INC TST                 .. TST=#06
        LDI A.0(START) ;PLO PTR .. PTR=START
WILE6:  GLO PTR
        BNZ BODY6
        GHI PTR
        BZ OK
BODY6:  GHI PC ;PLO VAL         .. VAL=#00
LOOP:   STR PTR                 .. SAVE VAL
        XOR
        BNZ FAIL
        INC VAL                 .. VAL=VAL+1
        GLO VAL
        BNZ LOOP                .. BACK TO LOOP IF VAL#0
        INC PTR
        BR WILE6
        ..
FAIL:   LDI #DE ;PHI 4          .. #4=#DEAD
        LDI #AD ;PLO 4
        IDL
OK:     LDI #60 ;PHI 4          .. #4=#600D
        LDI #0D ;PLO 4
        IDL                     .. STOP
..
..  WRALL - WRITE D TO ALL LOCS AND CHK
..  IF DF=1 INCREMENT VAL
..
RET1:   SEP PC                  .. RETURN
WRALL:  INC TST                 .. BUMP TEST#
        PLO VAL                 .. SAVE D
        LDI A.0(START) ;PLO PTR .. PTR=START
WILE1:  GLO PTR                 .. PTR=0 ?
        BNZ BODY1
        GHI PTR
        BZ RET1
BODY1:  GLO VAL ;STR PTR        .. SAVE VAL
        XOR                     .. CHK
        BNZ FAIL                .. FAIL IF NEQU
        INC PTR                 .. INCR. PTR
        BNF WILE1               .. IF DF=1
        INC VAL                 ..   INCR.VAL
        BR WILE1
..
..  WRPAG - WRITE PAGE# TO ALL LOCS AND CHK
..  IF DF=1 INVERT PAGE NUMBER
..
RET2:   SEP PC                  .. RETURN
WRPAG:  INC TST                 .. BUMP TEST#
        LDI A.0(START) ;PLO PTR .. PTR=START
WILE2:  GLO PTR
        BNZ BODY2
        GHI PTR
        BZ RET2
BODY2:  GHI PTR                 .. GET PAGE#
        LSNF ;XRI #FF           .. IF DF=0 DON'T INVERT
        STR PTR                 .. SAVE PAGE#
        XOR                     .. CHK
        BNZ FAIL                .. FAIL IF NEQU
        INC PTR                 .. INCREMENT PTR
        BR WILE2

START:  NOP                     .. MEMTEST STARTS HERE
        END
